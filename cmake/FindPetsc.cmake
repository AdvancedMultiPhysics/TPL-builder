FUNCTION( PETSC_GET_VERSION PETSC_INCLUDE_DIR )
    IF ( EXISTS "${PETSC_INCLUDE_DIR}/petscversion.h" )
        FILE( STRINGS "${PETSC_INCLUDE_DIR}/petscversion.h" vstrings REGEX "#define PETSC_VERSION_(RELEASE|MAJOR|MINOR|SUBMINOR|PATCH) " )
        FOREACH( line ${vstrings} )
            STRING( REGEX REPLACE " +" ";" fields ${line} ) # break line into three fields ( the first is always "#define" )
            LIST( GET fields 1 var )
            LIST( GET fields 2 val )
            SET( ${var} ${val} PARENT_SCOPE )
            SET( ${var} ${val} ) # Also in local scope so we have access below
        ENDFOREACH()
        IF ( PETSC_VERSION_RELEASE )
            SET( PETSC_VERSION "${PETSC_VERSION_MAJOR}.${PETSC_VERSION_MINOR}.${PETSC_VERSION_SUBMINOR}p${PETSC_VERSION_PATCH}" PARENT_SCOPE )
        ELSE()
            # make dev version compare higher than any patch level of a released version
            SET( PETSC_VERSION "${PETSC_VERSION_MAJOR}.${PETSC_VERSION_MINOR}.${PETSC_VERSION_SUBMINOR}.99" PARENT_SCOPE )
        ENDIF()
    ELSE()
        MESSAGE( SEND_ERROR "PETSC_DIR can not be used, ${PETSC_DIR}/include/petscversion.h does not exist" )
    ENDIF()
ENDFUNCTION()

FUNCTION( PETSC_SET_LIBRARIES PETSC_LIB_DIRECTORY )
    FIND_LIBRARY( PETSC_LIB NAMES petsc PATHS ${PETSC_LIB_DIRECTORY} NO_DEFAULT_PATH )
    IF ( (NOT PETSC_LIB ) )
        MESSAGE( ${PETSC_LIB} )
        MESSAGE( FATAL_ERROR "PETsc libraries not found in ${PETSC_LIB_DIRECTORY}" )
    ENDIF()
    SET( PETSC_LIBS ${PETSC_LIB} )
    SET( PETSC_LIBS ${PETSC_LIBS} PARENT_SCOPE )
ENDFUNCTION()
