# This will configure and build petaca
# User can configure the source path by specifying PETACA_SRC_DIR,
#    the download path by specifying PETACA_URL, or the installed 
#    location by specifying PETACA_INSTALL_DIR


# Intialize download/src/install vars
SET( PETACA_BUILD_DIR "${CMAKE_BINARY_DIR}/PETACA-prefix/src/PETACA-build" )
IF ( PETACA_URL ) 
    MESSAGE_TPL("   PETACA_URL = ${PETACA_URL}")
    SET( PETACA_SRC_DIR "${CMAKE_BINARY_DIR}/PETACA-prefix/src/PETACA-src" )
    SET( PETACA_CMAKE_URL            "${PETACA_URL}"     )
    SET( PETACA_CMAKE_DOWNLOAD_DIR   "${PETACA_SRC_DIR}" )
    SET( PETACA_CMAKE_SOURCE_DIR     "${PETACA_SRC_DIR}" )
    SET( PETACA_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/petaca" )
    SET( CMAKE_BUILD_PETACA TRUE )
ELSEIF ( PETACA_SRC_DIR )
    VERIFY_PATH("${PETACA_SRC_DIR}")
    MESSAGE_TPL("   PETACA_SRC_DIR = ${PETACA_SRC_DIR}")
    SET( PETACA_CMAKE_URL            ""                  )
    SET( PETACA_CMAKE_DOWNLOAD_DIR   ""                  )
    SET( PETACA_CMAKE_SOURCE_DIR     "${PETACA_SRC_DIR}" )
    SET( PETACA_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/petaca" )
    SET( CMAKE_BUILD_PETACA TRUE )
ELSEIF ( PETACA_INSTALL_DIR ) 
    SET( PETACA_CMAKE_INSTALL_DIR "${PETACA_INSTALL_DIR}" )
    SET( CMAKE_BUILD_PETACA FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify PETACA_SRC_DIR, PETACA_URL, or PETACA_INSTALL_DIR")
ENDIF()
SET( PETACA_INSTALL_DIR "${PETACA_CMAKE_INSTALL_DIR}" )
MESSAGE_TPL( "   PETACA_INSTALL_DIR = ${PETACA_INSTALL_DIR}" )
FILE( APPEND "${CMAKE_INSTALL_PREFIX}/TPLs.cmake" "SET(PETACA_INSTALL_DIR \"${PETACA_INSTALL_DIR}\")\n" )


# Configure petaca
IF ( CMAKE_BUILD_PETACA )
    SET( CONFIGURE_OPTIONS "${CMAKE_ARGS};-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}/petaca" )
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DYAJL_LIBRARY_DIR=${CMAKE_INSTALL_PREFIX}/yajl/lib" )
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DYAJL_INCLUDE_DIR=${CMAKE_INSTALL_PREFIX}/yajl/include" )
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DCMAKE_Fortran_COMPILER_VERSION=18.0.0" )
ENDIF()


# Build petaca
IF ( CMAKE_BUILD_PETACA )
    EXTERNALPROJECT_ADD(
        PETACA
        URL                 "${PETACA_CMAKE_URL}"
        DOWNLOAD_DIR        "${PETACA_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${PETACA_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
        BUILD_IN_SOURCE     0
        INSTALL_DIR         ${CMAKE_INSTALL_PREFIX}/petaca
        CMAKE_ARGS          "${CONFIGURE_OPTIONS}"
        BUILD_COMMAND       make install -j ${PROCS_INSTALL} VERBOSE=1
        DEPENDS             YAJL
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )
    ADD_TPL_SAVE_LOGS( PETACA )
    ADD_TPL_CLEAN( PETACA )
ELSE()
    ADD_TPL_EMPTY( PETACA )
ENDIF()


