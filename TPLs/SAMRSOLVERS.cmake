# This will configure and build SAMRSolvers
# User can configure the source path by specifying SAMRSOLVERS_SRC_DIR,
#    the download path by specifying SAMRSOLVERS_URL, or the installed 
#    location by specifying SAMRSOLVERS_INSTALL_DIR


# Intialize download/src/install vars
SET( SAMRSOLVERS_BUILD_DIR "${CMAKE_BINARY_DIR}/SAMRSOLVERS-prefix/src/SAMRSOLVERS-build" )
IF ( SAMRSOLVERS_URL ) 
    MESSAGE_TPL("   SAMRSOLVERS_URL = ${SAMRSOLVERS_URL}")
    SET( SAMRSOLVERS_SRC_DIR "${CMAKE_BINARY_DIR}/SAMRSOLVERS-prefix/src/SAMRSOLVERS-src" )
    SET( SAMRSOLVERS_CMAKE_URL            "${SAMRSOLVERS_URL}"     )
    SET( SAMRSOLVERS_CMAKE_DOWNLOAD_DIR   "${SAMRSOLVERS_SRC_DIR}" )
    SET( SAMRSOLVERS_CMAKE_DOWNLOAD_CMD   URL                 )
    SET( SAMRSOLVERS_CMAKE_SOURCE_DIR     "${SAMRSOLVERS_SRC_DIR}" )
    SET( SAMRSOLVERS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/samrsolvers" )
    SET( CMAKE_BUILD_SAMRSOLVERS TRUE )
ELSEIF ( SAMRSOLVERS_SRC_DIR )
    VERIFY_PATH("${SAMRSOLVERS_SRC_DIR}")
    MESSAGE_TPL("   SAMRSOLVERS_SRC_DIR = ${SAMRSOLVERS_SRC_DIR}")
    SET( SAMRSOLVERS_CMAKE_URL            ""                  )
    SET( SAMRSOLVERS_CMAKE_DOWNLOAD_DIR   ""                  )
    SET( SAMRSOLVERS_CMAKE_DOWNLOAD_CMD   URL                 )
    SET( SAMRSOLVERS_CMAKE_SOURCE_DIR     "${SAMRSOLVERS_SRC_DIR}" )
    SET( SAMRSOLVERS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/samrsolvers" )
    SET( CMAKE_BUILD_SAMRSOLVERS TRUE )
ELSEIF ( SAMRSOLVERS_INSTALL_DIR ) 
    SET( SAMRSOLVERS_CMAKE_INSTALL_DIR "${SAMRSOLVERS_INSTALL_DIR}" )
    SET( CMAKE_BUILD_SAMRSOLVERS FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify SAMRSOLVERS_SRC_DIR, SAMRSOLVERS_URL, or SAMRSOLVERS_INSTALL_DIR")
ENDIF()
FILE( MAKE_DIRECTORY "${SAMRSOLVERS_CMAKE_INSTALL_DIR}" )
SET( SAMRSOLVERS_INSTALL_DIR "${SAMRSOLVERS_CMAKE_INSTALL_DIR}" )
MESSAGE_TPL( "   SAMRSOLVERS_INSTALL_DIR = ${SAMRSOLVERS_INSTALL_DIR}" )
FILE( APPEND "${CMAKE_INSTALL_PREFIX}/TPLs.cmake" "SET(SAMRSOLVERS_INSTALL_DIR \"${SAMRSOLVERS_INSTALL_DIR}\")\n" )


# Configure SAMRSolvers
IF ( CMAKE_BUILD_SAMRSOLVERS )
    SET( CONFIGURE_OPTIONS "${CMAKE_ARGS}" )
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DSAMRUTILS_DIRECTORY=${SAMRUTILS_INSTALL_DIR}" )
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DSAMRSOLVERS_INSTALL_DIR=${SAMRSOLVERS_INSTALL_DIR}" )
ENDIF()


# Build SAMRSolvers
IF ( CMAKE_BUILD_SAMRSOLVERS )
    EXTERNALPROJECT_ADD(
        SAMRSOLVERS
        URL                 "${SAMRSOLVERS_CMAKE_URL}"
        DOWNLOAD_DIR        "${SAMRSOLVERS_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${SAMRSOLVERS_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
        CMAKE_ARGS          "${CONFIGURE_OPTIONS}"
        BUILD_COMMAND       make -j ${PROCS_INSTALL} VERBOSE=1
        BUILD_IN_SOURCE     0
        INSTALL_COMMAND     make install
        DEPENDS             SAMRUTILS
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )
    ADD_TPL_SAVE_LOGS( SAMRSOLVERS )
    ADD_TPL_CLEAN( SAMRSOLVERS )
ELSE()
    ADD_TPL_EMPTY( SAMRSOLVERS )
ENDIF()


