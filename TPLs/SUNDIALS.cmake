# This will configure and build sundials
# User can configure the source path by specifying SUNDIALS_SRC_DIR,
#    the download path by specifying SUNDIALS_URL, or the installed 
#    location by specifying SUNDIALS_INSTALL_DIR


# Intialize download/src/install vars
SET( SUNDIALS_BUILD_DIR "${CMAKE_BINARY_DIR}/SUNDIALS-prefix/src/SUNDIALS-build" )
IF ( SUNDIALS_URL ) 
    MESSAGE_TPL("   SUNDIALS_URL = ${SUNDIALS_URL}")
    SET( SUNDIALS_SRC_DIR "${CMAKE_BINARY_DIR}/SUNDIALS-prefix/src/SUNDIALS-src" )
    SET( SUNDIALS_CMAKE_URL            "${SUNDIALS_URL}"     )
    SET( SUNDIALS_CMAKE_DOWNLOAD_DIR   "${SUNDIALS_SRC_DIR}" )
    SET( SUNDIALS_CMAKE_SOURCE_DIR     "${SUNDIALS_SRC_DIR}" )
    SET( SUNDIALS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/sundials" )
    SET( CMAKE_BUILD_SUNDIALS TRUE )
ELSEIF ( SUNDIALS_SRC_DIR )
    VERIFY_PATH("${SUNDIALS_SRC_DIR}")
    MESSAGE_TPL("   SUNDIALS_SRC_DIR = ${SUNDIALS_SRC_DIR}")
    SET( SUNDIALS_CMAKE_URL            ""                  )
    SET( SUNDIALS_CMAKE_DOWNLOAD_DIR   ""                  )
    SET( SUNDIALS_CMAKE_SOURCE_DIR     "${SUNDIALS_SRC_DIR}" )
    SET( SUNDIALS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/sundials" )
    SET( CMAKE_BUILD_SUNDIALS TRUE )
ELSEIF ( SUNDIALS_INSTALL_DIR ) 
    SET( SUNDIALS_CMAKE_INSTALL_DIR "${SUNDIALS_INSTALL_DIR}" )
    SET( CMAKE_BUILD_SUNDIALS FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify SUNDIALS_SRC_DIR, SUNDIALS_URL, or SUNDIALS_INSTALL_DIR")
ENDIF()
SET( SUNDIALS_INSTALL_DIR "${SUNDIALS_CMAKE_INSTALL_DIR}" )
MESSAGE_TPL( "   SUNDIALS_INSTALL_DIR = ${SUNDIALS_INSTALL_DIR}" )
FILE( APPEND "${CMAKE_INSTALL_PREFIX}/TPLs.cmake" "SET(SUNDIALS_INSTALL_DIR \"${SUNDIALS_INSTALL_DIR}\")\n" )

# Configure sundials
IF ( CMAKE_BUILD_SUNDIALS )
    SET( CONFIGURE_OPTIONS "${CMAKE_ARGS};-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}/sundials" )
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}" )
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DMPI_ENABLE:BOOL=ON" )
    # the mpi compiler needs to be explicitly set else the parallel nvec libraries are not built
    # for now assuming the C compiler is the MPI compiler
    SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DMPI_MPICC=${CMAKE_C_COMPILER}" )
    IF ( ENABLE_SHARED AND ENABLE_STATIC )
        MESSAGE(FATAL_ERROR "Compiling sundials with both static and shared libraries is not yet supported")
    ELSEIF ( ENABLE_SHARED )
        SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DBUILD_SHARED_LIBS:BOOL=ON" )
    ELSEIF ( ENABLE_STATIC )
        SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};-DBUILD_STATIC_LIBS:BOOL=ON" )
    ENDIF()
    STRING( REPLACE "," ";" SUNDIALS_EXTRA_FLAGS "${SUNDIALS_EXTRA_FLAGS}" )
    FOREACH( flags ${SUNDIALS_EXTRA_FLAGS} )
        SET( CONFIGURE_OPTIONS "${CONFIGURE_OPTIONS};${flags}" )
    ENDFOREACH()
ENDIF()


# Configure sundials
IF ( CMAKE_BUILD_SUNDIALS )
    EXTERNALPROJECT_ADD(
        SUNDIALS
        URL                 "${SUNDIALS_CMAKE_URL}"
        DOWNLOAD_DIR        "${SUNDIALS_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${SUNDIALS_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
        BUILD_IN_SOURCE     0
        INSTALL_DIR         ${CMAKE_INSTALL_PREFIX}/sundials
        CMAKE_ARGS          "${CONFIGURE_OPTIONS}"
        BUILD_COMMAND       ${CMAKE_MAKE_PROGRAM} install ${PARALLEL_BUILD_OPTIONS} VERBOSE=1
        DEPENDS             ${SUNDIALS_DEPENDS}
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )
    ADD_TPL_SAVE_LOGS( SUNDIALS )
    ADD_TPL_CLEAN( SUNDIALS )
ELSE()
    ADD_TPL_EMPTY( SUNDIALS )
ENDIF()


# Add the appropriate fields to FindTPLs.cmake
CONFIGURE_FILE( ${CMAKE_CURRENT_SOURCE_DIR}/cmake/FindSundials.cmake "${CMAKE_INSTALL_PREFIX}/cmake/FindSundials.cmake" COPYONLY )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n# Find SUNDIALS\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "IF ( TPLs_FIND_SUNDIALS AND NOT TPL_FOUND_SUNDIALS )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    INCLUDE( \"${CMAKE_INSTALL_PREFIX}/cmake/FindSundials.cmake\" )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( SUNDIALS_DIR \"${SUNDIALS_INSTALL_DIR}\" )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( SUNDIALS_DIRECTORY \"${SUNDIALS_INSTALL_DIR}\" )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SUNDIALS_SET_INCLUDES( ${SUNDIALS_INSTALL_DIR} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SUNDIALS_SET_LIBRARIES( ${SUNDIALS_INSTALL_DIR} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( TPL_INCLUDE_DIRS $\{TPL_INCLUDE_DIRS} $\{SUNDIALS_INCLUDE} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( TPL_LIBRARIES $\{SUNDIALS_LIBS} $\{TPL_LIBRARIES} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( SUNDIALS_FOUND TRUE )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( TPL_FOUND_SUNDIALS TRUE )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n" )


