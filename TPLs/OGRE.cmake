# This will configure and build ogre
# User can configure the source path by specifying OGRE_SRC_DIR,
#    the download path by specifying OGRE_URL, or the installed 
#    location by specifying OGRE_INSTALL_DIR


# Intialize download/src/install vars
SET( OGRE_BUILD_DIR "${CMAKE_BINARY_DIR}/OGRE-prefix/src/OGRE-build" )
IF ( OGRE_URL ) 
    MESSAGE("   OGRE_URL = ${OGRE_URL}")
    SET( OGRE_SRC_DIR "${CMAKE_BINARY_DIR}/OGRE-prefix/src/OGRE-src" )
    SET( OGRE_CMAKE_URL            "${OGRE_URL}"     )
    SET( OGRE_CMAKE_DOWNLOAD_DIR   "${OGRE_SRC_DIR}" )
    SET( OGRE_CMAKE_SOURCE_DIR     "${OGRE_SRC_DIR}" )
    SET( OGRE_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/ogre" )
    SET( CMAKE_BUILD_OGRE TRUE )
ELSEIF ( OGRE_SRC_DIR )
    VERIFY_PATH("${OGRE_SRC_DIR}")
    MESSAGE("   OGRE_SRC_DIR = ${OGRE_SRC_DIR}")
    SET( OGRE_CMAKE_URL            ""                  )
    SET( OGRE_CMAKE_DOWNLOAD_DIR   ""                  )
    SET( OGRE_CMAKE_SOURCE_DIR     "${OGRE_SRC_DIR}" )
    SET( OGRE_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/ogre" )
    SET( CMAKE_BUILD_OGRE TRUE )
ELSEIF ( OGRE_INSTALL_DIR ) 
    SET( OGRE_CMAKE_INSTALL_DIR "${OGRE_INSTALL_DIR}" )
    SET( CMAKE_BUILD_OGRE FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify OGRE_SRC_DIR, OGRE_URL, or OGRE_INSTALL_DIR")
ENDIF()
SET( OGRE_INSTALL_DIR "${OGRE_CMAKE_INSTALL_DIR}" )
MESSAGE( "   OGRE_INSTALL_DIR = ${OGRE_INSTALL_DIR}" )


# Configure optional/required TPLs
CONFIGURE_DEPENDENCIES( OGRE OPTIONAL BOOST )


# Configure ogre
IF ( CMAKE_BUILD_OGRE )
    SET( OGRE_CONFIGURE_OPTIONS "${CMAKE_ARGS};-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}/ogre" )
    SET( OGRE_CONFIGURE_OPTIONS "${OGRE_CONFIGURE_OPTIONS};-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}" )
    SET( OGRE_DEPENDENCIES_DIR "${BOOST_INSTALL_DIR}" )
    SET( OGRE_CONFIGURE_OPTIONS "${OGRE_CONFIGURE_OPTIONS};-DBoost_NO_BOOST_CMAKE=TRUE;-DBoost_NO_SYSTEM_PATHS=TRUE" )
    SET( OGRE_CONFIGURE_OPTIONS "${OGRE_CONFIGURE_OPTIONS};-DBOOST_ROOT=${BOOST_INSTALL_DIR};-DBOOST_LIBRARYDIR=${BOOST_INSTALL_DIR}/lib" )
    SET( OGRE_CONFIGURE_OPTIONS "${OGRE_CONFIGURE_OPTIONS};-DBoost_LIBRARY_DIR_DEBUG=${BOOST_INSTALL_DIR}/lib;-DBoost_LIBRARY_DIR_RELEASE=${BOOST_INSTALL_DIR}/lib" )
    #IF ( ENABLE_SHARED AND ENABLE_STATIC )
    #    MESSAGE(FATAL_ERROR "Compiling libmesh with both static and shared libraries is not supported")
    #ELSEIF ( ENABLE_SHARED )
    #    SET( OGRE_CONFIGURE_OPTIONS ${OGRE_CONFIGURE_OPTIONS} )
    #ELSEIF ( ENABLE_STATIC )
    #    SET( OGRE_CONFIGURE_OPTIONS ${OGRE_CONFIGURE_OPTIONS};-DOGRE_STATIC=TRUE;-DBoost_USE_STATIC_LIBS=TRUE )
    #ENDIF()
    IF ( BOOST_ONLY_COPY_HEADERS )
        MESSAGE(FATAL_ERROR "Ogre requires the boost libraries")
    ENDIF()


    # Build ogre
    ADD_TPL(
        OGRE
        URL                 "${OGRE_CMAKE_URL}"
        DOWNLOAD_DIR        "${OGRE_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${OGRE_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
        BUILD_IN_SOURCE     0
        INSTALL_DIR         ${CMAKE_INSTALL_PREFIX}/ogre
        CMAKE_ARGS          "${OGRE_CONFIGURE_OPTIONS}"
        BUILD_COMMAND       $(MAKE) install VERBOSE=1
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )
    EXTERNALPROJECT_ADD_STEP(
        OGRE
        install-bullet
        COMMAND             ${CMAKE_COMMAND} -E make_directory "${CMAKE_INSTALL_PREFIX}/ogre/include/bullet"
        COMMENT             ""
        DEPENDEES           install
        LOG                 0
    )

ELSE()
    ADD_TPL_EMPTY( OGRE )
ENDIF()


# Add the appropriate fields to FindTPLs.cmake
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n# Find OGRE\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "IF ( TPLs_FIND_OGRE AND NOT TPLs_OGRE_FOUND )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    FIND_PACKAGE( OGRE REQUIRED PATHS \"${OGRE_INSTALL_DIR}\" COMPONENTS Bites CONFIG )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    ADD_TPL_LIBRARY( OGRE $\{OGRE_LIBRARIES} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n" )


