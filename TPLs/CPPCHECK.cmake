# This will configure and build cppcheck
# User can configure the source path by specifying CPPCHECK_SRC_DIR,
#    the download path by specifying CPPCHECK__URL, or the installed 
#    location by specifying CPPCHECK__INSTALL_DIR


# Intialize download/src/install vars
SET( CPPCHECK_BUILD_DIR "${CMAKE_BINARY_DIR}/CPPCHECK-prefix/src/CPPCHECK-build" )
IF ( CPPCHECK_URL ) 
    MESSAGE("   CPPCHECK_URL = ${CPPCHECK_URL}")
    SET( CPPCHECK_SRC_DIR "${CMAKE_BINARY_DIR}/CPPCHECK-prefix/src/CPPCHECK-src" )
    SET( CPPCHECK_CMAKE_URL            "${CPPCHECK_URL}"     )
    SET( CPPCHECK_CMAKE_DOWNLOAD_DIR   "${CPPCHECK_SRC_DIR}" )
    SET( CPPCHECK_CMAKE_SOURCE_DIR     "${CPPCHECK_SRC_DIR}" )
    SET( CPPCHECK_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/cppcheck" )
    SET( CMAKE_BUILD_CPPCHECK TRUE )
ELSEIF ( CPPCHECK_SRC_DIR )
    VERIFY_PATH("${CPPCHECK_SRC_DIR}")
    MESSAGE("   CPPCHECK_SRC_DIR = ${CPPCHECK_SRC_DIR}")
    SET( CPPCHECK_CMAKE_URL            ""                  )
    SET( CPPCHECK_CMAKE_DOWNLOAD_DIR   ""                  )
    SET( CPPCHECK_CMAKE_SOURCE_DIR     "${CPPCHECK_SRC_DIR}" )
    SET( CPPCHECK_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/cppcheck" )
    SET( CMAKE_BUILD_CPPCHECK TRUE )
ELSEIF ( CPPCHECK_INSTALL_DIR ) 
    SET( CPPCHECK_CMAKE_INSTALL_DIR "${CPPCHECK_INSTALL_DIR}" )
    SET( CMAKE_BUILD_CPPCHECK FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify CPPCHECK_SRC_DIR, CPPCHECK_URL, or CPPCHECK_INSTALL_DIR")
ENDIF()
SET( CPPCHECK_INSTALL_DIR "${CPPCHECK_CMAKE_INSTALL_DIR}" )
MESSAGE( "   CPPCHECK_INSTALL_DIR = ${CPPCHECK_INSTALL_DIR}" )


# Configure cppcheck
IF ( CMAKE_BUILD_CPPCHECK )
    EXTERNALPROJECT_ADD(
        CPPCHECK
        URL                 "${CPPCHECK_CMAKE_URL}"
        TIMEOUT             300
        DOWNLOAD_DIR        "${CPPCHECK_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${CPPCHECK_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
        BUILD_IN_SOURCE     0
        INSTALL_DIR         ${CMAKE_INSTALL_PREFIX}/cppcheck
        CMAKE_ARGS          -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER} -DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}/cppcheck -DCMAKE_BUILD_TYPE=Release
        BUILD_COMMAND       ${CMAKE_MAKE_PROGRAM} install ${PARALLEL_BUILD_OPTIONS} VERBOSE=1
        DEPENDS             
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )
    EXTERNALPROJECT_ADD_STEP(
        CPPCHECK
        clean
        COMMAND             make clean
        DEPENDEES           install
        WORKING_DIRECTORY   "${CPPCHECK_BUILD_DIR}"
        LOG                 1
    )
    ADD_TPL_SAVE_LOGS( CPPCHECK )
    ADD_TPL_CLEAN( CPPCHECK )
ELSE()
    ADD_TPL_EMPTY( CPPCHECK )
ENDIF()


# Add the appropriate fields to FindTPLs.cmake
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n# Find CPPCHECK\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "SET( CPPCHECK_DIRECTORY ${CMAKE_INSTALL_PREFIX}/cppcheck )\n"  )
FILE( APPEND "${FIND_TPLS_CMAKE}" "IF ( TPLs_FIND_CPPCHECK AND NOT TPL_FOUND_CPPCHECK )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( CPPCHECK_FOUND TRUE )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( TPL_FOUND_CPPCHECK TRUE )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n" )

