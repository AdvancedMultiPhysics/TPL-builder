# This will configure and build parmetis
# User can configure the source path by specifying PARMETIS_SRC_DIR,
#    the download path by specifying PARMETIS_URL, or the installed 
#    location by specifying PARMETIS_INSTALL_DIR


# Intialize download/src/install vars
SET( PARMETIS_BUILD_DIR "${CMAKE_BINARY_DIR}/PARMETIS-prefix/src/PARMETIS-build" )
IF ( PARMETIS_URL ) 
    MESSAGE("   PARMETIS_URL = ${PARMETIS_URL}")
    SET( PARMETIS_CMAKE_URL            "${PARMETIS_URL}"       )
    SET( PARMETIS_CMAKE_DOWNLOAD_DIR   "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_SOURCE_DIR     "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/parmetis" )
    SET( CMAKE_BUILD_PARMETIS TRUE )
ELSEIF ( PARMETIS_SRC_DIR )
    VERIFY_PATH("${PARMETIS_SRC_DIR}")
    MESSAGE("   PARMETIS_SRC_DIR = ${PARMETIS_SRC_DIR}")
    SET( PARMETIS_CMAKE_URL            "${PARMETIS_SRC_DIR}"   )
    SET( PARMETIS_CMAKE_DOWNLOAD_DIR   "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_SOURCE_DIR     "${PARMETIS_BUILD_DIR}" )
    SET( PARMETIS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/parmetis" )
    SET( CMAKE_BUILD_PARMETIS TRUE )
ELSEIF ( PARMETIS_INSTALL_DIR ) 
    SET( PARMETIS_CMAKE_INSTALL_DIR "${PARMETIS_INSTALL_DIR}" )
    SET( CMAKE_BUILD_PARMETIS FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify PARMETIS_SRC_DIR, PARMETIS_URL, or PARMETIS_INSTALL_DIR")
ENDIF()
SET( PARMETIS_INSTALL_DIR "${PARMETIS_CMAKE_INSTALL_DIR}" )
MESSAGE( "   PARMETIS_INSTALL_DIR = ${PARMETIS_INSTALL_DIR}" )

# Configure optional/required TPLs
CONFIGURE_DEPENDENCIES( PARMETIS REQUIRED METIS GKLIB )

# Configure parmetis
IF ( CMAKE_BUILD_PARMETIS )
    SET( PARMETIS_CONFIGURE_OPTIONS prefix=${PARMETIS_INSTALL_DIR} )
    SET( PARMETIS_CONFIGURE_OPTIONS ${PARMETIS_CONFIGURE_OPTIONS} cc=${CMAKE_C_COMPILER} )
    SET( PARMETIS_CONFIGURE_OPTIONS ${PARMETIS_CONFIGURE_OPTIONS} gklib_path=${GKLIB_INSTALL_DIR} )
    SET( PARMETIS_CONFIGURE_OPTIONS ${PARMETIS_CONFIGURE_OPTIONS} metis_path=${METIS_INSTALL_DIR} )
    IF ( ENABLE_SHARED AND ENABLE_STATIC )
        MESSAGE(FATAL_ERROR "Compiling parmetis with both static and shared libraries is not yet supported")
    ELSEIF ( ENABLE_SHARED )
        SET( PARMETIS_CONFIGURE_OPTIONS ${METIS_CONFIGURE_OPTIONS} shared=1 )
#    ELSEIF ( ENABLE_STATIC )
    ENDIF()
    MESSAGE("   PARMETIS configure options: ${PARMETIS_CONFIGURE_OPTIONS}")

    # Build parmetis
    # Note: a bug in the parmetis cmake scripts results in parmetis not installing the metis headers and libs
    # The current fix is to modify the Parmetis CMakeLists.txt to run the metis install also
    # Currently this might not be portable due to the use of sed
    ADD_TPL(
        PARMETIS
        URL                 "${PARMETIS_CMAKE_URL}"
        DOWNLOAD_DIR        "${PARMETIS_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${PARMETIS_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
#        PATCH_COMMAND       sed -e "s|add_subdirectory\(\${METIS_PATH}\\/libmetis \${CMAKE_BINARY_DIR}\\/libmetis\)|add_subdirectory\(\${METIS_PATH}\)|" ${PARMETIS_CMAKE_SOURCE_DIR}/CMakeLists.txt > tmp
#                            COMMAND mv tmp ${PARMETIS_CMAKE_SOURCE_DIR}/CMakeLists.txt
        CONFIGURE_COMMAND   $(MAKE) config ${PARMETIS_CONFIGURE_OPTIONS} VERBOSE=1
        BUILD_COMMAND       $(MAKE) -i
        BUILD_IN_SOURCE     1
        INSTALL_COMMAND     $(MAKE) install -i
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )

ELSE()
    ADD_TPL_EMPTY( PARMETIS )
ENDIF()


FILE( APPEND "${FIND_TPLS_CMAKE}" "\n# Find PARMETIS\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "IF ( TPLs_FIND_PARMETIS AND NOT TPLs_PARMETIS_FOUND )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( PARMETIS_INSTALL_DIR \"${PARMETIS_INSTALL_DIR}\" )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( PARMETIS_LIB_DIR \"${PARMETIS_INSTALL_DIR}/lib\" )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    FIND_LIBRARY( PARMETIS_LIB  NAMES parmetis  PATHS $\{PARMETIS_LIB_DIR}  NO_DEFAULT_PATH )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    ADD_TPL_LIBRARY( PARMETIS $\{PARMETIS_LIB} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n" )

