# This will configure and build lapack wrappers
# User can configure the source path by specifying LAPACK_WRAPPERS_SRC_DIR
#    the download path by specifying LAPACK_WRAPPERS_URL, or the installed 
#    location by specifying LAPACK_WRAPPERS_INSTALL_DIR


# Intialize download/src/install vars
SET( LAPACK_WRAPPERS_BUILD_DIR "${CMAKE_BINARY_DIR}/LAPACK_WRAPPERS-prefix/src/LAPACK_WRAPPERS-build" )
IF ( LAPACK_WRAPPERS_URL ) 
    MESSAGE("   LAPACK_WRAPPERS_URL = ${LAPACK_WRAPPERS_URL}")
    SET( LAPACK_WRAPPERS_SRC_DIR "${CMAKE_BINARY_DIR}/LAPACK_WRAPPERS-prefix/src/LAPACK_WRAPPERS-src" )
    SET( LAPACK_WRAPPERS_CMAKE_URL            "${LAPACK_WRAPPERS_URL}"     )
    SET( LAPACK_WRAPPERS_CMAKE_DOWNLOAD_DIR   "${LAPACK_WRAPPERS_SRC_DIR}" )
    SET( LAPACK_WRAPPERS_CMAKE_DOWNLOAD_CMD   URL                )
    SET( LAPACK_WRAPPERS_CMAKE_SOURCE_DIR     "${LAPACK_WRAPPERS_SRC_DIR}" )
    SET( LAPACK_WRAPPERS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/lapackwrappers" )
    SET( CMAKE_BUILD_LAPACK_WRAPPERS TRUE )
ELSEIF ( LAPACK_WRAPPERS_SRC_DIR )
    VERIFY_PATH("${LAPACK_WRAPPERS_SRC_DIR}")
    MESSAGE("   LAPACK_WRAPPERS_SRC_DIR = ${LAPACK_WRAPPERS_SRC_DIR}")
    SET( LAPACK_WRAPPERS_CMAKE_URL            ""                  )
    SET( LAPACK_WRAPPERS_CMAKE_DOWNLOAD_DIR   ""                  )
    SET( LAPACK_WRAPPERS_CMAKE_DOWNLOAD_CMD   URL                 )
    SET( LAPACK_WRAPPERS_CMAKE_SOURCE_DIR     "${LAPACK_WRAPPERS_SRC_DIR}" )
    SET( LAPACK_WRAPPERS_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/lapackwrappers" )
    SET( CMAKE_BUILD_LAPACK_WRAPPERS TRUE )
ELSEIF ( LAPACK_WRAPPERS_INSTALL_DIR ) 
    SET( LAPACK_WRAPPERS_CMAKE_INSTALL_DIR "${LAPACK_WRAPPERS_INSTALL_DIR}" )
    SET( CMAKE_BUILD_LAPACK_WRAPPERS FALSE )
    # Check that the libraries exist
    FIND_LIBRARY(LAPACK_WRAPPERS_LIB lapackwrappers "${LAPACK_WRAPPERS_INSTALL_DIR}/lib")
    IF ( NOT LAPACK_WRAPPERS_LIB )
        MESSAGE(FATAL_ERROR "LapackWrapper library does not exist in \"${LAPACK_WRAPPERS_INSTALL_DIR}/lib\"")
    ENDIF()
ELSE()
    MESSAGE(FATAL_ERROR "Please specify LAPACK_WRAPPERS_SRC_DIR, LAPACK_WRAPPERS_URL, or LAPACK_WRAPPERS_INSTALL_DIR")
ENDIF()
FILE( MAKE_DIRECTORY "${LAPACK_WRAPPERS_CMAKE_INSTALL_DIR}" )
SET( LAPACK_WRAPPERS_INSTALL_DIR "${LAPACK_WRAPPERS_CMAKE_INSTALL_DIR}" )
MESSAGE( "   LAPACK_WRAPPERS_INSTALL_DIR = ${LAPACK_WRAPPERS_INSTALL_DIR}" )


# Configure optional/required TPLs
CONFIGURE_DEPENDENCIES( LAPACK_WRAPPERS OPTIONAL LAPACK )


IF ( CMAKE_BUILD_LAPACK_WRAPPERS )

    # Check if we have Lapack
    SET( LAPACK_WRAPPERS_OPTIONS "${CMAKE_ARGS}" )
    SET( LAPACK_WRAPPERS_OPTIONS "${LAPACK_WRAPPERS_OPTIONS};-DTPL_DIRECTORY=${CMAKE_INSTALL_PREFIX}" )
    SET( LAPACK_WRAPPERS_OPTIONS "${LAPACK_WRAPPERS_OPTIONS};-DINSTALL_DIR=${LAPACK_WRAPPERS_INSTALL_DIR}" )
    IF ( NOT LAPACK_CONFIGURED )
        SET( LAPACK_WRAPPERS_OPTIONS "${LAPACK_WRAPPERS_OPTIONS};-DDISABLE_BLAS=TRUE" )
    ENDIF()
    MESSAGE( "LAPACK_WRAPPERS_OPTIONS=${LAPACK_WRAPPERS_OPTIONS}") 

    # Configure lapack wrappers
    ADD_TPL(
        LAPACK_WRAPPERS
        URL                 "${LAPACK_WRAPPERS_CMAKE_URL}"
        DOWNLOAD_DIR        "${LAPACK_WRAPPERS_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${LAPACK_WRAPPERS_CMAKE_SOURCE_DIR}"
        UPDATE_COMMAND      ""
        BUILD_IN_SOURCE     0
        INSTALL_DIR         "${LAPACK_WRAPPERS_INSTALL_DIR}"
        CMAKE_ARGS          "${LAPACK_WRAPPERS_OPTIONS}"
        BUILD_COMMAND       $(MAKE) install  VERBOSE=1
        DEPENDS             "${LAPACK_WRAPPERS_DEPENDS}"
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )

ENDIF()


# Add the appropriate fields to FindTPLs.cmake
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n# Find LAPACK_WRAPPERS\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "IF ( TPLs_FIND_LAPACK_WRAPPERS AND NOT TPL_FOUND_LAPACK_WRAPPERS )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( TPL_INCLUDE_DIRS \"${LAPACK_WRAPPERS_INSTALL_DIR}/include\" $\{TPL_INCLUDE_DIRS} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    FIND_LIBRARY( LAPACK_WRAPPERS_LIB  NAMES LapackWrappers PATHS \"${LAPACK_WRAPPERS_INSTALL_DIR}/lib\"  NO_DEFAULT_PATH )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    IF ( NOT LAPACK_WRAPPERS_LIB )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "        MESSAGE( FATAL_ERROR \"LapackWrappers library not found in ${LAPACK_WRAPPERS_INSTALL_DIR}/lib\" )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( CMAKE_INSTALL_RPATH $\{CMAKE_INSTALL_RPATH} \"${LAPACK_WRAPPERS_INSTALL_DIR}/lib\" )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( TPL_LIBRARIES $\{LAPACK_WRAPPERS_LIB} $\{TPL_LIBRARIES} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( TPL_FOUND_LAPACK_WRAPPERS TRUE )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n" )

