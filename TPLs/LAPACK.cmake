# This will configure and build lapack
# User can configure the source path by specifying LAPACK_SRC_DIR
#    the download path by specifying LAPACK_URL, or the installed 
#    location by specifying LAPACK_INSTALL_DIR


CONFIGURE_DEPENDENCIES( LAPACK OPTIONAL OPENBLAS )


# Skip if we are installing OpenBLAS
IF ( OPENBLAS_INSTALL_DIR )
    RETURN()
ENDIF()


# Override options if we are using MATLAB's lapack
IF ( USE_MATLAB_LAPACK )
    LIST( FIND TPL_LIST "LAPACK" index1 )
    LIST( FIND TPL_LIST "MATLAB" index2 )
    IF ( ${index2} GREATER ${index1} )
        MESSAGE(FATAL_ERROR "MATLAB should be specified before LAPACK when USE_MATLAB_LAPACK is enabled" )
    ENDIF()
    SET( LAPACK_INSTALL_DIR "${MATLAB_EXTERN}" )
ENDIF()


# Intialize download/src/install vars
SET( LAPACK_BUILD_DIR "${CMAKE_BINARY_DIR}/LAPACK-prefix/src/LAPACK-build" )
IF ( LAPACK_URL ) 
    MESSAGE("   LAPACK_URL = ${LAPACK_URL}")
    SET( LAPACK_CMAKE_URL            "${LAPACK_URL}"       )
    SET( LAPACK_CMAKE_DOWNLOAD_DIR   "${LAPACK_BUILD_DIR}" )
    SET( LAPACK_CMAKE_SOURCE_DIR     "${LAPACK_BUILD_DIR}" )
    SET( LAPACK_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/lapack" )
    SET( CMAKE_BUILD_LAPACK TRUE )
ELSEIF ( LAPACK_SRC_DIR )
    MESSAGE("   LAPACK_SRC_DIR = ${LAPACK_SRC_DIR}")
    SET( LAPACK_CMAKE_URL                                  )
    SET( LAPACK_CMAKE_DOWNLOAD_DIR                         )
    SET( LAPACK_CMAKE_SOURCE_DIR     "${LAPACK_SRC_DIR}"   )
    SET( LAPACK_CMAKE_INSTALL_DIR "${CMAKE_INSTALL_PREFIX}/lapack" )
    SET( CMAKE_BUILD_LAPACK TRUE )
ELSEIF ( LAPACK_INSTALL_DIR ) 
    SET( LAPACK_CMAKE_INSTALL_DIR "${LAPACK_INSTALL_DIR}" )
    SET( CMAKE_BUILD_LAPACK FALSE )
ELSE()
    MESSAGE(FATAL_ERROR "Please specify LAPACK_SRC_DIR, LAPACK_URL, or LAPACK_INSTALL_DIR")
ENDIF()
NULL_USE(   BLAS_INSTALL_DIR   BLAS_SRC_DIR   BLAS_URL_DIR   BLAS_LIB )
NULL_USE( LAPACK_INSTALL_DIR LAPACK_SRC_DIR LAPACK_URL_DIR LAPACK_LIB )
FILE( MAKE_DIRECTORY "${LAPACK_CMAKE_INSTALL_DIR}" )
SET( LAPACK_INSTALL_DIR "${LAPACK_CMAKE_INSTALL_DIR}" )
MESSAGE( "   LAPACK_INSTALL_DIR = ${LAPACK_INSTALL_DIR}" )
SET( LAPACK_OUT "${CMAKE_CURRENT_BINARY_DIR}/tmp/LAPACK_OUT.cmake" )
FILE( WRITE "${LAPACK_OUT}" "" )
FILE( APPEND "${LAPACK_OUT}" "    SET(LAPACK_INSTALL_DIR \"${LAPACK_INSTALL_DIR}\")\n" )


# Build lapack
IF ( CMAKE_BUILD_LAPACK ) 
    ADD_TPL(
        LAPACK
        URL                 "${LAPACK_CMAKE_URL}"
        DOWNLOAD_COMMAND    URL
        DOWNLOAD_DIR        "${LAPACK_CMAKE_DOWNLOAD_DIR}"
        SOURCE_DIR          "${LAPACK_SRC_DIR}"
        UPDATE_COMMAND      ""
        BUILD_IN_SOURCE     0
        INSTALL_DIR         "${LAPACK_INSTALL_DIR}"
        CMAKE_ARGS          "${CMAKE_ARGS};-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}/lapack"
        BUILD_COMMAND       $(MAKE) install VERBOSE=1
        LOG_DOWNLOAD 1   LOG_UPDATE 1   LOG_CONFIGURE 1   LOG_BUILD 1   LOG_TEST 1   LOG_INSTALL 1
    )
    SET( LAPACK_VENDOR "Generic" )
    SET( BLAS_DIR   "${LAPACK_INSTALL_DIR}/${CMAKE_INSTALL_LIBDIR}" )
    SET( LAPACK_DIR "${LAPACK_INSTALL_DIR}/${CMAKE_INSTALL_LIBDIR}" )
    IF ( ENABLE_SHARED )
        SET( BLAS_LIBS   "${BLAS_DIR}/libblas.so" )
        SET( LAPACK_LIBS "${LAPACK_DIR}/liblapack.so" )
    ELSE()
        SET( BLAS_LIBS   "${BLAS_DIR}/libblas.a" )
        SET( LAPACK_LIBS "${LAPACK_DIR}/liblapack.a" )
    ENDIF()
    SET( BLAS_LAPACK_LINK "${LAPACK_LIBS}" "${BLAS_LIBS}" )
ELSE()
    ADD_TPL_EMPTY( LAPACK )
    SET( CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/../cmake" ${CMAKE_MODULE_PATH} )
    FIND_PACKAGE( BlasLapack REQUIRED )
    IF ( (NOT BLAS_FOUND) OR (NOT LAPACK_FOUND) )
        MESSAGE(FATAL_ERROR "No sutable blas or lapack libraries found in ${LAPACK_INSTALL_DIR}")
    ENDIF()
ENDIF()
IF ( NOT LAPACK_INCLUDE_DIRS )
    SET( LAPACK_INCLUDE_DIRS "${LAPACK_INSTALL_DIR}/include" )
ENDIF()


# Add the appropriate fields to TPLs.cmake and FindTPLs.cmake
STRING( REPLACE ";" " " BLAS_LIBS2 "${BLAS_LIBS}" )
STRING( REPLACE ";" " " LAPACK_LIBS2 "${LAPACK_LIBS}" )
FILE( READ "${LAPACK_OUT}" LAPACK_OUTPUT_FIELDS )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n# Find LAPACK\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "IF ( TPLs_FIND_LAPACK AND LAPACK_FOUND )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    ADD_TPL_LIBRARY( LAPACK ${BLAS_LAPACK_LINK} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "IF ( TPLs_FIND_LAPACK AND NOT TPLs_LAPACK_FOUND )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( LAPACK_FOUND $\{USE_LAPACK} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( LAPACK_INSTALL_DIR ${LAPACK_INSTALL_DIR} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( LAPACK_INCLUDE_DIRS ${LAPACK_INCLUDE_DIRS} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( LAPACK_LIB_DIR ${LAPACK_LIB_DIR} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( LAPACK_VENDOR ${LAPACK_VENDOR} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( BLAS_DIR ${BLAS_DIR} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( LAPACK_DIR ${LAPACK_DIR} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( BLAS_INCLUDE_DIRS ${BLAS_INCLUDE_DIRS} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( BLAS_LIBS ${BLAS_LIBS2} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( LAPACK_LIBS ${LAPACK_LIBS2} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    SET( BLAS_LAPACK_LINK ${BLAS_LAPACK_LINK} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "    ADD_TPL_LIBRARY( LAPACK ${BLAS_LAPACK_LINK} )\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "ENDIF()\n" )
FILE( APPEND "${FIND_TPLS_CMAKE}" "\n" )


